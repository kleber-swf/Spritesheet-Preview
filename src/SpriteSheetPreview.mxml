<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009"
					   xmlns:s="library://ns.adobe.com/flex/spark"
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   xmlns:provider="com.pblabs.engine.resource.provider.*"
					   width="780" height="520" minWidth="780" minHeight="520"
					   applicationComplete="application_applicationComplete(event)"
					   backgroundColor="#5A5E67"
					   creationComplete="application_creationComplete(event)" currentState="Initial"
					   enterFrame="application_enterFrame(event)" frameRate="60"
					   invoke="application_invoke(event)" showStatusBar="false"
					   title="SpriteSheet Preview"
					   width.Initial="972"
					   nativeDragDrop.Initial="application_nativeDragDrop(event)"
					   nativeDragEnter.Initial="application_nativeDragEnter(event)">
	<s:states>
		<s:State name="Initial"/>
		<s:State name="About"/>
	</s:states>

	<fx:Style>
		@namespace s "library://ns.adobe.com/flex/spark";
		@namespace mx "library://ns.adobe.com/flex/mx";
		@namespace provider "com.pblabs.engine.resource.provider.*";
		
		.Tools {
			backgroundColor:#444444;
			borderColor:#333333;
			shadowColor: #323232;
			dropShadowVisible:true;
		}
	</fx:Style>

	<fx:Script>
		<![CDATA[
			import app.AnimationModel;
			import app.ResourceController;
			import app.SpriteSheet;
			import app.UpdateController;
			
			import mx.collections.ArrayList;
			import mx.controls.Alert;
			import mx.core.IUIComponent;
			import mx.events.FlexEvent;
			import mx.events.ResizeEvent;
			import mx.managers.DragManager;
			
			import spark.events.IndexChangeEvent;
			
			private static const WHEEL_PAGE_SIZE:int = 3;
			
			[Bindable] private var _model:AnimationModel;
			[Bindable] private var _updater:UpdateController;
			
			private var _resources:ResourceController;
			
			public static var _spriteSheet:SpriteSheet;
			private var _bitmapData:BitmapData;
			private var _drawBitmap:Bitmap;
			private var _buffer:BitmapData;
			private var _matrix:Matrix = new Matrix();
			
			[Bindable] private var _scale:Number = 1;
			[Bindable] private var _minScale:Number = 0.5;
			[Bindable] private var _maxScale:Number = 3;
			[Bindable] private var _hasContent:Boolean;
			[Bindable] private var _frames:ArrayList;
			[Bindable] private var _showFrameBorder:Boolean;
			
			private var _lastTime:int = 0;
			private var _lastDirectory:String;
			
			
			/* ===== [INITIALIZATION] ===== */
			
			private function application_creationComplete(event:FlexEvent):void {
				// Check for updates
				_updater = new UpdateController("http://kleber.flipflopgames.com/projects/tools/spritesheetpreview/update.xml");
				_updater.checkForUpdate(function(event:ErrorEvent):void {Alert.show(event.toString());});
			}
			
			private function application_applicationComplete(event:FlexEvent):void {
				// Create instances
				_model = new AnimationModel();
				_resources = new ResourceController();
				_resources.onImageLoadSuccess = onImageLoadSuccess;
				_resources.onImageLoadError = onImageLoadError;
				
				_spriteSheet = new SpriteSheet(_model);
				createDrawBitmap();
				stageContainer.addChild(_drawBitmap);
				
				// install keyboard shortcuts
				stage.addEventListener(KeyboardEvent.KEY_UP, onKeyUp, false, int.MAX_VALUE);
				stage.addEventListener(MouseEvent.MOUSE_WHEEL, onMouseWheel, false, int.MAX_VALUE);
			}
			
			private function application_invoke(event:InvokeEvent):void {
				// Check initial parameters
				var arguments:Array = event.arguments;
				if (arguments == null || arguments.length <= 0)
					return;
				_resources.imageURL = arguments[0];
				if (arguments.length > 0) {
					var n:Number = parseInt(arguments[1]);
					if (!isNaN(n))
						_model.rows = n;
					if (arguments.length > 1) {
						n = parseInt(arguments[2]);
						if (!isNaN(n))
							_model.cols = n;
					}
					setupFrames();
				}
			}
			
			
			/* ===== [EVENTS] ===== */
			
			private function stageContainer_resize(event:ResizeEvent):void {
				if (stageContainer == null || stageContainer.width == 0 || stageContainer.height == 0)
					return;
				var newWidth:int = stageContainer.width;
				var newHeight:int = stageContainer.height;
				createDrawBitmap();
				scale = _scale;
			}
			
			private function zoomSlider_change(event:Event):void {
				scale = (event.target as HSlider).value;
			}
			
			private function zoomLabel_change(event:Event):void {
				scale = valueChanged(event) * 0.01;
			}
			
			private function contentContainer_click(event:MouseEvent):void {
				event.target.setFocus();
			}
			
			private function application_enterFrame(event:Event):void {
				if (_bitmapData == null || _spriteSheet == null)
					return;
				
				var time:int = getTimer();
				var delta:int = time - _lastTime;
				_lastTime = time;
				
				_buffer.lock();
				_buffer.fillRect(_buffer.rect, 0x000000);
				_spriteSheet.draw(_buffer, delta, _buffer.rect);
				_buffer.unlock();
				
				_bitmapData.lock();
				_bitmapData.fillRect(_bitmapData.rect, 0x000000);
				if (_showFrameBorder)
					drawFrameBorder(_bitmapData);
				_bitmapData.draw(_buffer, _matrix);
				
				
				_bitmapData.unlock();
			}
			
			private function openButton_click(event:MouseEvent):void {
				if (!_lastDirectory)
					_lastDirectory = File.desktopDirectory.url;
				var file:File = new File(_lastDirectory);
				file.addEventListener(FileListEvent.SELECT_MULTIPLE, openFile);
				file.browseForOpenMultiple("Choose the sprites");
			}
			
			private function openFile(event:FileListEvent):void {
				_resources.imageURL = event.files[0].url;
			}
			
			// TODO this could be better...
			private function onKeyUp(event:KeyboardEvent):void {
				switch (event.keyCode) {
					case Keyboard.ENTER:
						if (!event.ctrlKey)
							return;
						togglePlay();
						event.stopPropagation();
						return;
					case Keyboard.L:
						toggleLoop();
						event.stopPropagation();
						return;
					case Keyboard.P:
						togglePingPong();
						event.stopPropagation();
						return;
				}
			}
			
			// TODO this could be better...
			private function onMouseWheel(event:MouseEvent):void {
				var focusedComponent:UIComponent = focusManager.getFocus() as UIComponent;
				if (!focusedComponent) {
					focusedComponent = stage.focus as UIComponent;
					if (!focusedComponent)
						return;
				}
				var focusedComponentID:String = focusedComponent.id;
				switch (focusedComponentID) {
					case "stageContainer":
						scale = _scale + (event.delta / WHEEL_PAGE_SIZE * zoomSlider.stepSize * 10);
						break;
				}
			}
			
			
			/* ===== [DRAG AND DROP] ===== */
			
			private function application_nativeDragEnter(event:NativeDragEvent):void {
				if (!event.clipboard.hasFormat(ClipboardFormats.FILE_LIST_FORMAT))
					return;
				var files:Array = event.clipboard.getData(ClipboardFormats.FILE_LIST_FORMAT) as Array;
				// only allow a single file to be dragged in
				if (files.length == 1) {
					var r:IUIComponent = event.currentTarget as IUIComponent;
					DragManager.acceptDragDrop(r);
				} else
					DragManager.acceptDragDrop(this);
			}
			
			private function application_nativeDragDrop(event:NativeDragEvent):void {
				var arr:Array = event.clipboard.getData(ClipboardFormats.FILE_LIST_FORMAT) as Array;
				_resources.imageURL = (arr[0] as File).url;
			}
			
			
			/* ===== [ANIMATION DATA CHANGING] ===== */
			
			private function rowsInput_change(event:Event):void {
				_model.rows = valueChanged(event);
				setupFrames();
			}
			
			private function colsInput_change(event:Event):void {
				_model.cols = valueChanged(event);
				setupFrames();
			}
			
			private function startFrameInput_change(event:Event):void {
				_model.startFrame = event.target.value -1;
				setupFrames();
			}
			
			private function endFrameInput_change(event:Event):void {
				_model.endFrame = valueChanged(event, event.target.value - 1);
				setupFrames();
			}
			
			private function fpsInput_change(event:Event):void {
				_model.fps = valueChanged(event);
			}
			
			private function loopCheckbox_changed(event:Event):void {
				toggleLoop();
			}
			
			private function pingpongCheckbox_changed(event:Event):void {
				togglePingPong();
			}
			
			private function playButton_change(event:Event):void {
				togglePlay();
			}
			
			private function frameBorderCheckBox_click(event:MouseEvent):void {
				_showFrameBorder = frameBorderCheckBox.selected;
			}
			
			private function frameSlider_change(event:Event):void {
				_model.playing = false;
				_model.currentFrame = (event.target as HSlider).value - 1;
			}
			
			private function frameList_change(event:IndexChangeEvent):void {
				_model.currentFrame = event.target.selectedItem;
			}
			
			
			/* ===== [OPERATIONS] ===== */
			
			private function set scale(value:Number):void {
				_scale = Math.max(_minScale, Math.min(_maxScale, value));
				_matrix.identity();
				_matrix.scale(_scale, _scale);
				_buffer = new BitmapData(stageContainer.width / _scale, stageContainer.height / _scale, true, 0x000000);
			}
			
			private function valueChanged(event:Event, value:Number = -1):int {
				value = value < 0 ? event.target.value : value;
				value = Math.max(1, isNaN(value) ? 1 : value);
				event.target.value = value;
				return value;
			}
			
			private function togglePlay():void {
				_model.playing = !_model.playing;
			}
			
			private function toggleLoop():void {
				_model.loop = !_model.loop;
			}
			
			private function togglePingPong():void {
				_model.pingpong = !_model.pingpong;
				if (!_model.pingpong)
					_spriteSheet.resetPlayDirection();
			}
			
			private function createDrawBitmap():void {
				var data:BitmapData = new BitmapData(stageContainer.width, stageContainer.height, true, 0x000000);
				if (_drawBitmap == null)
					_drawBitmap = new Bitmap(data, "auto", true);
				else {
					data.copyPixels(_bitmapData, _bitmapData.rect, new Point());
					_drawBitmap.bitmapData.dispose();
					_drawBitmap.bitmapData = data;
				}
				_buffer = new BitmapData(data.width, data.height, true, 0x000000);
				_bitmapData = data;
			}
			
			private function onImageLoadError(errString:String):void {
				Alert.show(errString, "Error");
			}
			
			private function onImageLoadSuccess(image:Bitmap):void {
				_spriteSheet.image = image;
				_hasContent = true;
			}
			
			private function setupFrames():void {
				_frames = new ArrayList(_model.frames);
				frameList.selectedItem = _model.currentFrame;
			}
			
			private function drawFrameBorder(buffer:BitmapData):void {
				var s:Sprite = new Sprite();
				s.graphics.clear();
				s.graphics.lineStyle(1, 0xFFFFFF - backgroundColorPicker.selectedColor, 0.9);
				s.graphics.drawRect(_spriteSheet.drawPosition.x * _scale -1, _spriteSheet.drawPosition.y * _scale -1, _spriteSheet.width * _scale + 2, _spriteSheet.height * _scale + 2);
				buffer.draw(s);
			}
			
		]]>
	</fx:Script>
	<s:BorderContainer id="topBar" left="160" right="87" top="10" height="32" enabled="{_hasContent}"
					   styleName="Tools"
					   left.Initial="160" right.Initial="86" top.Initial="10">

		<s:CheckBox id="loopCheckbox" x="102" label="Loop" change="loopCheckbox_changed(event)"
					selected="{_model.loop}" toolTip="L" verticalCenter="0"/>
		<s:CheckBox id="pingpongCheckbox" x="159" label="Ping Pong"
					change="pingpongCheckbox_changed(event)" selected="{_model.pingpong}"
					toolTip="P" verticalCenter="0"/>
		<s:NumericStepper id="fpsInput" x="10" width="52" change="fpsInput_change(event)"
						  maximum="100" minimum="1" textAlign="right" value="{_model.fps}"
						  verticalCenter="0"
						  height.About="20"
						  borderColor.Initial="#333333" borderVisible.Initial="true"/>
		<s:Label x="67" text="fps" verticalCenter="0"/>
		<mx:Button id="playButton" x="254" change="playButton_change(event)"
				   downSkin="@Embed('../assets/images/play-down.png')"
				   overSkin="@Embed('../assets/images/play-over.png')" selected="{_model.playing}"
				   selectedDownSkin="@Embed('../assets/images/pause-down.png')"
				   selectedOverSkin="@Embed('../assets/images/pause-over.png')"
				   selectedUpSkin="@Embed('../assets/images/pause.png')" toggle="true"
				   toolTip="Ctrl + Enter" upSkin="@Embed('../assets/images/play.png')"
				   verticalCenter="0"
				   x.Initial="355" verticalCenter.Initial="0"/>
		<s:HSlider id="frameSlider" left="284" right="10" change="frameSlider_change(event)"
				   dataTipPrecision="0" liveDragging="true" maximum="{_model.endFrame+1}"
				   minimum="{_model.startFrame+1}" stepSize="1" value="{_model.currentFrame+1}"
				   verticalCenter="0"
				   left.Initial="381" right.Initial="10" verticalCenter.Initial="0"/>
	</s:BorderContainer>
	<s:BorderContainer id="contentContainer" left="160" right="10" top="50" bottom="50"
					   borderVisible="false" click="contentContainer_click(event)"
					   dropShadowVisible="false"
					   left.Initial="160" right.Initial="193" top.Initial="50" bottom.Initial="50">
		<s:BitmapImage id="transparentBackground" left="0" right="0" top="0" bottom="0"
					   fillMode="repeat" source="@Embed('../assets/images/transparency.png')"/>
		<s:BorderContainer id="background" left="0" right="0" top="0" bottom="0"
						   backgroundAlpha="{backgroundAlphaSlider.value}"
						   backgroundColor="{backgroundColorPicker.selectedColor}"
						   borderVisible="false" dropShadowVisible="false"/>
		<s:BitmapImage id="dragAndDropImage" visible="{!_hasContent}" horizontalCenter="0"
					   source="@Embed('../assets/images/drag-n-drop.png')" verticalCenter="0"/>
		<mx:UIComponent id="stageContainer" left="0" right="0" top="0" bottom="0"
						resize="stageContainer_resize(event)"/>
	</s:BorderContainer>

	<s:BorderContainer id="bottomBar" left="160" right="10" bottom="10" height="32"
					   enabled="{_hasContent}" styleName="Tools"
					   left.Initial="160" right.Initial="10" bottom.Initial="10">
		<s:HSlider id="zoomSlider" right="74" change="zoomSlider_change(event)" maximum="{_maxScale}"
				   minimum="{_minScale}" showDataTip="false" stepSize="0.01" value="{_scale}"
				   verticalCenter="0"/>
		<s:NumericStepper id="zoomInput" right="10" width="52" change="zoomLabel_change(event)"
						  maximum="300" minimum="50" textAlign="right"
						  value="{(zoomSlider.value*100)}" verticalCenter="0"
						  borderColor.Initial="#333333"/>
		<s:Label x="10" text="Background" verticalCenter="0"/>
		<mx:ColorPicker id="backgroundColorPicker" x="82" selectedColor="#5A5E67"
						showTextField="true" verticalCenter="0"/>
		<s:HSlider id="backgroundAlphaSlider" x="112" width="60" maximum="1" minimum="0"
				   showDataTip="false" stepSize="0.01" value="1" verticalCenter="0"/>
		<s:CheckBox id="frameBorderCheckBox" includeIn="Initial" left="194" label="Show frame border"
					click="frameBorderCheckBox_click(event)" selected="{_showFrameBorder}"
					verticalCenter="0"/>
	</s:BorderContainer>
	<s:BorderContainer y="10" right="10" width="69" height="31" styleName="Tools"
					   y.Initial="10" right.Initial="10">
		<s:Button id="aboutButton" x="6" width="55" height="18" label="About" fontSize="10"
				  verticalCenter="0"
				  click.Initial="currentState = 'About'"/>
	</s:BorderContainer>
	<s:BorderContainer left="10" top="10" bottom="10" width="142" styleName="Tools">
		<s:Panel left="0" right="0" top="0" height="124" backgroundColor="#444444"
				 dropShadowVisible="false" enabled="{_hasContent}" styleName="Tools" title="Info"
				 left.Initial="0" right.Initial="0" top.Initial="92">
			<s:Label x="9" y="19" text="Rows"/>
			<s:Label x="9" y="49" text="Cols"/>
			<s:NumericStepper id="rowsInput" y="13" right="10" width="61"
							  change="rowsInput_change(event)" maximum="1000" minimum="1"
							  textAlign="right" value="{_model.rows}"
							  borderColor.Initial="#333333" borderVisible.Initial="true"/>
			<s:NumericStepper id="colsInput" y="41" right="10" width="61"
							  change="colsInput_change(event)" maximum="1000" minimum="1"
							  textAlign="right" value="{_model.cols}"
							  borderColor.Initial="#333333" borderVisible.Initial="true"/>
		</s:Panel>
		<s:Panel y="121" left="0" right="0" height="119" dropShadowVisible="false"
				 enabled="{_hasContent}" styleName="Tools" title="Frames"
				 y.Initial="215" left.Initial="0" right.Initial="0">
			<s:Label x="9" y="20" text="Start"/>
			<s:Label x="9" y="50" text="End"/>
			<s:NumericStepper id="startFrameInput" x="67" y="14" right="10" width="61"
							  change="startFrameInput_change(event)" maximum="{_model.totalFrames}"
							  minimum="1" textAlign="right" value="{_model.startFrame+1}"
							  borderColor.Initial="#333333" borderVisible.Initial="true"/>
			<s:NumericStepper id="endFrameInput" x="67" y="42" right="10" width="61"
							  change="endFrameInput_change(event)" maximum="{_model.totalFrames}"
							  minimum="{_model.startFrame+1}" textAlign="right"
							  value="{_model.endFrame+1}"
							  borderColor.Initial="#333333" borderVisible.Initial="true"/>
		</s:Panel>
		<s:Panel includeIn="Initial" y="0" left="0" right="0" height="93" dropShadowVisible="false"
				 styleName="Tools" title="File">
			<s:Button id="openButton" y="20" label="Open" click="openButton_click(event)"
					  horizontalCenter="0"/>
		</s:Panel>
	</s:BorderContainer>

	<s:BorderContainer includeIn="About" left="0" right="0" top="0" bottom="0" backgroundAlpha="0.81"
					   backgroundColor="#434343"
					   click.About="currentState = 'Initial'">
		<s:BorderContainer width="344" height="193" backgroundColor="#666666" horizontalCenter="-4"
						   styleName="Tools" verticalCenter="0">
			<s:Label y="15" color="#F3F3F3" fontFamily="Arial" fontSize="20" fontStyle="italic"
					 fontWeight="bold" horizontalCenter="7" text="SpriteSheet Preview"/>
			<mx:HRule y="39" width="264" height="4" horizontalCenter="18"/>
			<s:Label y="55" width="300" color="#F3F3F3" horizontalCenter="0"
					 text="A simple program to preview sprite sheets.&#xd;Just drag your spritesheet into the indicated area and set rows and cols. Hit play e see the preview."
					 textAlign="center"/>
			<s:Label bottom="33" color="#F3F3F3" horizontalCenter="0" text="Kleber Lopes da Silva"/>
			<mx:LinkButton bottom="12" label="http://kleber-swf.blogspot.com"
						   click="navigateToURL(new URLRequest('http://kleber-swf.blogspot.com'))"
						   color="#9AAEC4" horizontalCenter="0"/>
			<s:Image x="11" y="12" source="@Embed('assets/icons/icon-36.png')"/>
			<s:Label id="versionLabel" y="109" width="165" color="#F3F3F3" horizontalCenter="0"
					 text="Version {_updater.version}" textAlign="center"/>
		</s:BorderContainer>
	</s:BorderContainer>
	<s:BorderContainer includeIn="Initial" right="10" top="50" bottom="50" width="175"
					   styleName="Tools">
		<s:List id="frameList" left="10" right="10" top="10" bottom="10"
				change="frameList_change(event)" dataProvider="{_frames}"
				itemRenderer="FrameListItemRenderer" selectedItem="{_model.currentFrame}">
		</s:List>
	</s:BorderContainer>
</s:WindowedApplication>
